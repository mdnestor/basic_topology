import basic_topology.Separation
import basic_topology.MetricTopology
import basic_topology.Net

set_option linter.style.multiGoal false

variable {X Y D: Type*}

-- limit of a sequence in terms of the tail
def tail (x: Nat → X) (t: Nat): Nat → X :=
  fun n => x (t + n)
--helpers when using tails--
theorem tail_0 (s: Nat → X): tail s 0 = s := by
 funext n
 simp [tail]

theorem tail_m_n (s: Nat → X)(m n :Nat)(hmn: m≥n): (tail s n) (m-n) = s m   := by
  simp[tail]
  simp_all only [ge_iff_le, Nat.add_sub_cancel']
--Convergence of sequences--
def converges (T: Family X) (x: Nat → X) (l: X): Prop :=
  ∀ N ∈ Nbhds T l, ∃ t, Set.range (tail x t) ⊆ N

def convergent (T: Family X) (x: Nat → X): Prop :=
  ∃ l, converges T x l

def converges_distance [DistanceSpaceStruct D] (d: X → X → D) (x: Nat → X) (l: X): Prop :=
  ∀ r, ⊥ < r → ∃ t, Set.range (tail x t) ⊆ openball d l r

def convergent_distance [DistanceSpaceStruct D] (d: X → X → D) (x: Nat → X): Prop :=
  ∃ l, converges_distance d x l
def increasing_seq (x: Nat → Nat ): Prop :=
  ∀ y1 y2 , x y1 > x y2 ↔ y1 > y2

def subsequence [DistanceSpaceStruct D] (x: Nat → X) (y: Nat → Nat ): (Nat→ X):=
x ∘ y

-- equivalent definition in a metric space
theorem converges_distance_iff [DistanceSpace D] (d: X → X → D) (hd: IsMetric d)(x: Nat → X) (l: X): converges (metric_open d) x l ↔ converges_distance d x l := by
  constructor
  · intro h r hr
    let N := openball d l r
    have h1: N ∈ Nbhds (metric_open d) l := by
      apply openball_neighborhood
      exact hd
      exact hr
    apply h
    exact h1
  · intro h
    simp [converges]
    intro N hN
    simp [converges_distance] at h
    simp [Nbhds, neighborhood] at hN
    obtain ⟨ U,hU₁, hU₂, hU₃⟩ := hN
    have h3: ∃r>0, openball d l r ⊆ U := by
      obtain ⟨r, hr₁, hr₂⟩ := hU₁ l hU₂
      exists r
      constructor
      exact pos_of_gt hr₁
      exact hr₂
    obtain ⟨ R,hR⟩ := h3
    have: R>0 := by
      exact hR.left
    apply h at this
    obtain ⟨ t,ht⟩ := this
    use t
    intro x hx
    apply hU₃
    apply hR.2
    apply ht
    exact hx

def adherent_value (T: Family X) (x: Nat → X) (a: X): Prop :=
  ∀ N ∈ Nbhds T a, ∀ t, (Set.range (tail x t) ∩ N).Nonempty

-- defn of a subsequence

-- a is adherent iff exists subsequence converging to a

-- limits are unique in a hausdorff space
theorem hausdorff_limit_unique_sequences (T: Family X) (h: hausdorff T) (x: Nat → X) (l1 l2: X) (h1: converges T x l1) (h2: converges T x l2): l1 = l2 := by
  by_contra h3
  simp[converges] at h1
  simp[converges] at h2
  simp[hausdorff] at h
  apply h at h3
  obtain ⟨U,hu⟩ := h3
  obtain ⟨ V,hv⟩ := hu.right
  let hu1 := hu.left
  apply h1 at hu1
  let hv1 := hv.left
  apply h2 at hv1
  obtain ⟨ t1,ht1⟩ := hu1
  obtain ⟨ t2,ht2⟩ := hv1
  set t := max t1 t2
  have htu: Set.range (tail x t)⊆ Set.range (tail x t1) := by
    intro y hy
    simp[Set.range]
    simp [Set.range] at hy
    obtain ⟨ y1,hy1⟩ := hy
    rw[tail] at hy1
    simp[tail]
    have: ∃m, t=m+ t1 := by
      refine Nat.exists_eq_add_of_le' ?_
      exact Nat.le_max_left t1 t2
    obtain ⟨ m,hm⟩ := this
    use m+y1
    rw[ Eq.symm (Nat.add_assoc t1 m y1)]
    rw[Nat.add_comm t1 m]
    rw[← hm]
    exact hy1
  have htv: Set.range (tail x t)⊆ Set.range (tail x t2) := by
    intro y hy
    simp[Set.range]
    simp [Set.range] at hy
    obtain ⟨ y1,hy1⟩ := hy
    rw[tail] at hy1
    simp[tail]
    have: ∃m, t=m+ t2 := by
      refine Nat.exists_eq_add_of_le' ?_
      exact Nat.le_max_right t1 t2
    obtain ⟨ m,hm⟩ := this
    use m+y1
    rw[ Eq.symm (Nat.add_assoc t2 m y1)]
    rw[Nat.add_comm t2 m]
    rw[← hm]
    exact hy1
  have hu1: Set.range ( tail x t ) ⊆ U := by exact fun ⦃a⦄ a_1 ↦ ht1 (htu a_1)
  have hv1: Set.range ( tail x t ) ⊆ V := by exact fun ⦃a⦄ a_1 ↦ ht2 (htv a_1)
  have huv1: ¬ (Disjoint U V) := by
    refine Set.not_disjoint_iff.mpr ?_
    use x (t+1)
    constructor
    apply hu1
    simp [Set.range, tail ]
    apply hv1
    simp [Set.range, tail ]
  tauto


-- prop: adherent points preserved by sequences

-- the set of adherent values are closed

-- defn of countable/denumerable set

--Sequences are nets--
theorem sequence_converges_iff_net_converges (T: Family X) (s : Nat → X) (L : X) : converges T s L ↔ net_converges T (· ≤ ·) s L := by
  rw[net_converges,converges]
  constructor
  intro hc N hN
  apply hc at hN
  obtain ⟨t,ht ⟩:= hN
  use t
  intro j hj
  apply ht
  simp
  use j-t
  exact tail_m_n s j t hj

  intro hU N hN
  apply hU at hN
  obtain ⟨t,ht ⟩:= hN
  use t
  intro x hx
  simp at hx
  obtain ⟨y, hy ⟩:= hx
  have : t+y≥ t:= by exact Nat.le_add_right t y
  apply ht at this
  rw[← hy]
  exact this
